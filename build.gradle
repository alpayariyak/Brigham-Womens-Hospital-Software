plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.gradle.spotless' version '3.27.1'
    id "io.freefair.lombok" version "5.0.0-rc6"
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}
mainClassName = 'edu.wpi.DapperDaemons.Main'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://apisite.crmyers.dev/maven'
    }
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.media']
}


dependencies {
    implementation(
            'com.jfoenix:jfoenix:9.0.10',

            // You may comment out the database dependency you do not use
            'org.xerial:sqlite-jdbc:3.30.1',
            'org.apache.derby:derby:10.15.2.0',
            'org.apache.derby:derbyclient:10.15.2.0',
            'org.apache.derby:derbytools:10.15.2.0',

            "com.twilio.sdk:twilio:8.28.0",

            'org.slf4j:slf4j-api:1.7.30',
            'org.slf4j:slf4j-simple:1.7.30',

            'com.google.firebase:firebase-admin:8.1.0',

            // https://mvnrepository.com/artifact/com.opencsv/opencsv
            [group: 'com.opencsv', name: 'opencsv', version: '3.7'],

            'me.xdrop:fuzzywuzzy:1.4.0'
    )

    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile(
            "org.testfx:testfx-core:4.0.16-alpha",
            'org.junit.jupiter:junit-jupiter:5.6.0',
            'org.testfx:testfx-junit5:4.0.16-alpha',
    )
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def codeCoverageExcludes = [
        'edu.wpi.DapperDaemons.App',
        'edu.wpi.DapperDaemons.Main',
]

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
            excludes = codeCoverageExcludes
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
            excludes = codeCoverageExcludes
        }

        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
            excludes = codeCoverageExcludes
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
compileJava.dependsOn 'spotlessApply'

spotless {
    java {
        googleJavaFormat()
    }
}

run {
    standardInput = System.in
}
